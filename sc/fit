#!/usr/bin/env gplpdf
set log x
set xtics 4
set xr [5:130]
set ytics nomirror
set key top left
set y2tics
unset link y2
!sc/extract > .fits.dat
plot '.fits.dat' u 1:2:3 w e t '$\beta_\mathrm{pc}$'
rep '.fits.dat' u 1:4:5 w e axes x1y2 t '$\chi_\mathrm{pc}$'

f(x)=betac-cbeta*x**(-1/nu)
fit f(x) '' u 1:2:3 via betac,cbeta,nu
rep f(x) axes x1y1 t '' lc 1

g(x)=cchi*x**(gamma/nu)
fit g(x) '' u 1:4:5 via cchi,gamma
rep g(x) axes x1y2 t '' lc 2

###MAKEPDF
! rm .fits.dat
! mv sc/fit.pdf plot/fit.pdf
print "beta_c = ", betac, " +- ",betac_err
print "    nu = ", nu, " +- ", nu_err
print " gamma = ", gamma, " +- ", gamma_err
